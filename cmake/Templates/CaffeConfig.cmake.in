# Config file for the Caffe package.
#
# Note:
#   Caffe and this config file depends on opencv,
#   so put `find_package(OpenCV)` before searching Caffe
#   via `find_package(Caffe)`. All other lib/includes
#   dependencies are hard coded in the file
#
# After successful configuration the following variables
# will be defined:
#
#   Caffe_LIBRARIES    - IMPORTED targets to link against
#                        (There is no Caffe_INCLUDE_DIRS and Caffe_DEFINITIONS
#                         because they are specified in the IMPORTED target interface.)
#
#   Caffe_HAVE_CUDA    - signals about CUDA support
#   Caffe_HAVE_CUDNN   - signals about cuDNN support


# OpenCV dependency (optional)

if(@USE_OPENCV@)
  if("@OpenCV_VERSION@" MATCHES "^2\\.")
    find_package(OpenCV REQUIRED COMPONENTS core highgui imgproc)
  else()
    find_package(OpenCV REQUIRED COMPONENTS core highgui imgproc imgcodecs)
  endif()
endif()

# Compute paths
get_filename_component(Caffe_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

# Our library dependencies
if(NOT TARGET caffe AND NOT caffe_BINARY_DIR)
  include("${Caffe_CMAKE_DIR}/CaffeTargets.cmake")
endif()

# List of IMPORTED libs created by CaffeTargets.cmake
# These targets already specify all needed definitions and include pathes
set(Caffe_LIBRARIES caffe)

# Cuda support variables
set(Caffe_CPU_ONLY @CPU_ONLY@)
set(Caffe_HAVE_CUDA @HAVE_CUDA@)
set(Caffe_HAVE_CUDNN @HAVE_CUDNN@)

# Dependencies {
find_package(Boost 1.46 REQUIRED COMPONENTS system thread filesystem)
find_package(Threads REQUIRED)
find_package(glog CONFIG REQUIRED)
find_package(gflags CONFIG REQUIRED)
find_package(Protobuf CONFIG REQUIRED)
find_package(ZLIB CONFIG REQUIRED)
#find_package(szip CONFIG REQUIRED)
#find_package(hdf5 CONFIG REQUIRED)

if(NOT APPLE)
  if("@BLAS@" STREQUAL "Atlas" OR "@BLAS@" STREQUAL "atlas")
    find_package(Atlas REQUIRED)
  elseif("@BLAS@" STREQUAL "Open" OR "@BLAS@" STREQUAL "open")
    find_package(OpenBLAS CONFIG REQUIRED)
  elseif("@BLAS@" STREQUAL "MKL" OR "@BLAS@" STREQUAL "mkl")
    find_package(MKL REQUIRED)
  endif()
elseif(APPLE)
  find_package(vecLib REQUIRED)
endif()
# }
